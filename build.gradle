/**
 * @author perry
 * stole from my private 1.8 client that he put this gradle on lulz
*/

plugins {
    id 'java'
    id 'de.fuerstenau.buildconfig' version '1.1.4'
    id 'io.freefair.lombok' version '6.4.3'
}

version = '1.0.0'
group = 'wtf.kull'
archivesBaseName = 'kull'

sourceCompatibility = targetCompatibility = '8'
compileJava {
    sourceCompatibility = targetCompatibility = '8'
    options.encoding 'UTF-8'
}


def getGitHash = { ->
    try {
        def stdout = new ByteArrayOutputStream()

        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }

        return stdout.toString().trim()
    } catch (ignored) {
        return "USE_FUCKING_GIT_RETARD"
    }
}

buildConfig {
    buildConfigField "String", "HASH", "${getGitHash()}"
    buildConfigField "String", "VERSION", "${project.version}"

    packageName "wtf.kull.util.versioning"
}

sourceSets.main.java.srcDirs += "build/gen/buildconfig/src/main"

// https://github.com/mfuerstenau/gradle-buildconfig-plugin/issues/30#issuecomment-910241307
configurations {
    create("compile")
}

// https://github.com/mfuerstenau/gradle-buildconfig-plugin/issues/30#issuecomment-910241307
tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.WARN
}

repositories {
    mavenCentral()
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        url = 'https://nexus.velocitypowered.com/repository/maven-public'
    }
    maven {
        url = 'https://repo.minebench.de/'
    }
}

configurations {
    embed
    implementation.extendsFrom embed
}

dependencies {
    // Brigadier
    embed 'com.mojang:brigadier:1.0.18'

    // microsoft account authentication
    embed 'com.github.litarvan:OpenAuth:1.1.2'

    // BUSHBUS!!!!!!
    embed 'com.github.therealbush:eventbus:1.0.2'

    // use all of the jars in the libs directory
    implementation fileTree('run/libraries')
}

jar {
    from(configurations.embed.collect {
        it.isDirectory() ? it : zipTree(it)
    }) {
        exclude 'module-info.class',
                'META-INF/maven/**',
                'META-INF/DEPENDENCIES**',
                'META-INF/LICENSE**',
                'META-INF/NOTICE**',
                'mozilla/**/*.txt',
                'org/**/*.txt',
                'org/**/*.properties'
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    manifest.attributes(
            'Main-Class': 'Start',
            'Class-Path': '.'
    )
}
